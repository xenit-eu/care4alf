apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    publications {
        all {
            pom {
                url = 'https://bitbucket.org/xenit/care4alf/'
                name = 'care4alf'
                description = 'Care4Alf â€” The Alfresco Swiss Army Knife'

                developers {
                    developer {
                        id = "xenit"
                        name = "Xenit Solutions NV"
                    }
                }

                scm {
                    connection = 'scm:git:git@bitbucket.org:xenit/care4alf.git'
                    developerConnection = 'scm:git:git@bitbucket.org:xenit/care4alf.git'
                    url = 'https://bitbucket.org/xenit/care4alf/'
                }

                licenses {
                    license {
                        name = 'GNU Lesser General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/lgpl-3.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        // Sonatype
        maven {
            name = 'Sonatype'
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

            credentials {
                username = project.findProperty('sonatype_username') ?: ''
                password = project.findProperty('sonatype_password') ?: ''
            }
        }
    }
}

static def hasPublish(TaskExecutionGraph graph) {
    graph.allTasks.any { it.name.startsWith("publish") }
}
static def shouldSign(Object version, TaskExecutionGraph graph) {
    !version.endsWith("SNAPSHOT") && hasPublish(graph)
}

afterEvaluate {
    signing {
        required { shouldSign(version, gradle.taskGraph) }
        useInMemoryPgpKeys(findProperty("signingKeyId"), findProperty("signingKey"), findProperty("signingPassword"))
        sign publishing.publications
    }
}
