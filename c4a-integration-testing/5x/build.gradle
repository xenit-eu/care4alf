plugins {
    id 'eu.xenit.docker-alfresco'// version '4.0.0'
}

archivesBaseName = 'c4a-integration-testing-5x'

sourceCompatibility = 1.8
targetCompatibility = 1.8

// Docker dependencies
dependencies {
    baseAlfrescoWar 'org.alfresco:alfresco-enterprise:5.1.4.1@war'
    alfrescoAmp "eu.xenit:alfresco-dynamic-extensions-repo:${deVersion}@amp"
    alfrescoAmp project(path: ':care4alf', configuration: 'amp')
}

dockerAlfresco {
    baseImage = 'hub.xenit.eu/alfresco-enterprise:5.1.4'
    leanImage = true
    dockerBuild {
        repository = 'hub.xenit.eu/care4alf-alfresco-51'
    }
}

jar {
    // Make a symlink at configuration time so the jar task finds the sources where it expects to find them
    ant.symlink(resource: project(':c4a-integration-testing').file("src/main/java"),
            link: project.file("src/main/java"), overwrite: true)

        bnd (
            'Export-Package': 'eu.xenit.care4alfintegration.*',
            'Alfresco-Dynamic-Extension': 'true',
            'Include-Resource': includeResource(configurations.compile),
            'Bundle-ClassPath': bundleClassPath(configurations.compile),
            // These are imported by rest-assured but not declared in the manifest
            'Import-Package': '!com.github.scribejava.*,!com.google.gson,' +
                    // These are the fault of groovy
                    '!org.apache.commons.cli,!org.apache.ivy.*,!org.fusesource.jansi,' +
                    // This is the fault of commons-logging
                    '!org.apache.log,' +
                    // This is the fault of mockito
                    '!org.apache.tools.ant.*,!org.junit.runners.*,!org.junit.runner.notification.*,' +
                    '!org.junit.internal.runners.*,!org.junit.rules.*,!org.mockito.asm.signature,*',
        )
    doLast {
        project.delete("src/main/java")
    }
}
