buildscript {
    ext.kotlin_version = '1.3.31'
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url 'https://artifactory.xenit.eu/artifactory/plugins-release'
            credentials {
                username property("eu.xenit.artifactory.username")
                password property("eu.xenit.artifactory.password")
            }
        }

        maven {
            url 'https://artifactory.xenit.eu/artifactory/libs-snapshot'
            credentials {
                username property("eu.xenit.artifactory.username")
                password property("eu.xenit.artifactory.password")
            }
        }
    }
    dependencies {
        classpath(group: 'eu.xenit.de', name: 'gradle-plugin', version: '2.0.1')
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'de.richsource.gradle.plugins:typescript-gradle-plugin:1.8.0'
        classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
        classpath "com.moowork.gradle:gradle-node-plugin:0.13"
    }
}

plugins {
    id 'eu.xenit.docker-alfresco' version '4.0.3'
    id 'eu.xenit.alfresco' version '0.2.0'
    id 'eu.xenit.amp' version '0.2.0'
}

allprojects {
    version = '2.0.0' + getVersionQualifier(System.env.BRANCH_NAME ?: 'local')
    group = 'eu.xenit'
}

//task wrapper(type: Wrapper) {
//    gradleVersion = '5.4.1'
//    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
//}


def bundleClassPath(configuration) {
    def list = ['.']
    configuration.each {
        list += 'lib/' + it.name
    }
    return list.join(',')
}
def includeResource(configuration) {
    def list = []
    configuration.each {
        list += 'lib/' + it.name + "=" + it.path
    }
    return list.join(',')
}
def includeUnrolledResource(configuration) {
    def list = []
    configuration.each {
        list += '@' + it.path
    }
    return list.join(',')
}
def importPackage() {
    return project("care4alf").file("importPackage").getText("UTF-8").replace("\n","")
}

ext.deVersion = '2.0.1'
ext.springVersion = '3.0.5.RELEASE'
ext.junitVersion = '4.12'
ext.buildNumber = project.hasProperty('buildNumber')?project.getProperty('buildNumber'):"SNAPSHOT";

subprojects {
    apply plugin: 'java'
}

def String getVersionQualifier(String branch_name) {
    if (project.hasProperty('qualifier')) {
        return '-' + project.getProperty('qualifier')
    } else if (branch_name == 'release') {
        return ''
    } else {
        return '-SNAPSHOT'
    }
}
