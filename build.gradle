ext.alfrescoVersion = '4.2.3.1'
ext.springVersion = '3.0.5.RELEASE'

buildscript {
    ext.kotlin_version = '0.10.4'
    repositories {
        jcenter()
        maven {
            url 'https://github.com/sothmann/typescript-gradle-plugin/raw/mavenrepo/'
        }
            }
    dependencies {
        classpath(group: 'com.github.dynamicextensionsalfresco', name: 'gradle-plugin', version: '1.1.+')
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'de.richsource.gradle.plugins:typescript-gradle-plugin:1.0.5'
        classpath 'de.obqo.gradle:gradle-lesscss-plugin:1.0-1.3.3'
    }
}

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'alfresco-dynamic-extension'
apply plugin: 'typescript'
apply plugin: 'lesscss'

[compileJava, compileTestJava,javadoc]*.options*.encoding = 'UTF-8'

compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
}

repositories {
    /*maven { url "http://artifactory.local.xenit.eu/artifactory/repo1" }
    maven { url "http://artifactory.local.xenit.eu/artifactory/alfresco-community" }
    maven { url "http://artifactory.local.xenit.eu/artifactory/spring-release" }
    maven { url "http://artifactory.local.xenit.eu/artifactory/libs-release-local" }
    maven { url 'http://artifactory.local.xenit.eu/artifactory/typesafe' }*/
    //mavenLocal()
    maven {
        credentials {
            username 'xenit'
            password '1pKo29#0rx'
        }
        url 'https://artifacts.alfresco.com/nexus/content/groups/private'
    }
    maven { url 'https://artifacts.alfresco.com/nexus/content/groups/public'}
    mavenCentral()
}

configurations {
    embedded
    compile.extendsFrom(embedded)
}

dependencies {
    compile ("org.alfresco:alfresco-core:${alfrescoVersion}") { transitive = false }
    compile ("org.alfresco:alfresco-repository:${alfrescoVersion}") { transitive = false }
    compile ("org.alfresco:alfresco-data-model:${alfrescoVersion}") { transitive = true }

    compile "org.springframework:spring-context:${project.springVersion}"
    compile "org.springframework:spring-jdbc:${project.springVersion}"
    compile "org.osgi:org.osgi.core:4.3.1"
    compile 'org.json:json:20090211'
    //compile("acegisecurity:acegi-security:0.8.2") {transitive = false}
    compile "org.slf4j:slf4j-api:1.7.5"
    compile 'org.eclipse.gemini:org.eclipse.gemini.blueprint.core:1.0.2.RELEASE'
    compile('javax.transaction:transaction-api:1.1') {transitive = false}
    compile('org.jibx:jibx-run:1.2.3') {transitive = false}
    compile('com.googlecode.json-simple:json-simple:1.1')
    embedded "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    embedded "org.jetbrains.kotlin:kotlin-jdbc:$kotlin_version"
}

compileTypeScript {
    sourcemap = false
    source = 'src/main/ts/eu/xenit/care4alf/web/js/care4alf.ts'
    out = file("${buildDir}/ts/eu/xenit/care4alf/web/js/care4alf.js")
}

lesscss {
    source = fileTree('src/main/less') {
        include '**/*.less'
    }
    dest = file("${buildDir}/resources/main/")
    compress = false
}

jar {
    from(configurations.embedded) {
        into('lib')
    }
    from(compileTypeScript.outputs)
    manifest {
        instruction 'Bundle-Classpath', ".,${configurations.embedded.files.collect({'lib/' + it.name}).join(',')}"
        instruction 'Import-Package', '!jet.*,!kotlin.*,!org.alfresco.repo.lock.mem,*'
    }
}

alfrescoDynamicExtensions {
    repository {
        endpoint {
            host = project.hasProperty('host') ? project.host : 'localhost'
            port = project.hasProperty('port') ? project.port : '8080'
            serviceUrl = '/alfresco/service'
        }
        authentication {
            username = project.hasProperty('username') ? project.username : 'admin'
            password = project.hasProperty('password') ? project.password : 'admin'
        }
    }
}

jar.dependsOn(['cleanCompileTypeScript', 'compileTypeScript', 'lesscss'])
